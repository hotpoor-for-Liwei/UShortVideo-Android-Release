apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    signingConfigs {
        config {
            storeFile file('/Users/tanhx/Documents/movieous/develop/movieous_sign')
            keyAlias 'movieous'
            keyPassword 'movieous'
            storePassword 'movieous'
        }
    }

    defaultConfig {
        applicationId "com.movieous.media"
        compileSdkVersion rootProject.ext.android.compileSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        minSdkVersion rootProject.ext.android.minSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        signingConfig signingConfigs.config

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
        multiDexEnabled true

        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.config
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.config
            zipAlignEnabled true
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "movieous_v${variant.versionName}_${variant.name}.apk"
        }
    }

    sourceSets {
        main.assets.srcDirs += ['src/main/assets', 'src/main/effects/bundle', 'src/main/makeup/bundle', "src/main/poster"]
        main.res.srcDirs += ['src/main/res', 'src/main/effects/res', 'src/main/filters/res', 'src/main/makeup/res']
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation files('libs/ushortvideo-1.0.0.jar')
    implementation files('libs/ufilesdk.jar')
    implementation project(path: ':faceunity')
    implementation project(path: ':STMobileJNI')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Support库
    implementation rootProject.ext.supportLibs
    // 网络请求库
    implementation rootProject.ext.networkLibs
    // RxJava2
    implementation rootProject.ext.rxJavaLibs
    implementation rootProject.ext.otherLibs
    // APT dependencies
    kapt rootProject.ext.annotationProcessorLibs
    //kotlin 支持库
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //GlideOkHttp
    implementation(rootProject.ext.glideOkhttp) {
        exclude group: 'glide-parent'
    }
    //Logger
    implementation 'com.orhanobut:logger:2.1.1'
    implementation project(':multiple-status-view')
    //运行时权限
    implementation 'pub.devrel:easypermissions:1.2.0'
    // 播放器
    implementation 'com.shuyu:GSYVideoPlayer:6.0.1'
    // Gif
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.7'
}
